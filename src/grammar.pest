selector = _{ SOI ~ jsonPath ~ EOI }

jsonPath = ${ rootSelector ~ matcher* }
rootSelector = { "$" }

matcher = { dotChild | union }

dotChild = _{ wildcardedDotChild | namedDotChild }
wildcardedDotChild = { ".*" }
namedDotChild = ${ "." ~ childName }
childName = @{ char+ }
char = {
    "-"
    | ASCII_DIGIT
    | ASCII_ALPHA
    | "_"
    | '\u{80}'..'\u{10FFFF}'
}

union =  { "[" ~ unionElement ~ ("," ~ unionElement)* ~ "]" }
unionElement = _{ unionChild | unionArrayIndex } // TODO: add unionArraySlice
unionChild = { doubleQuotedString | singleQuotedString }
unionArrayIndex = { "-" ? ~ ( "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* ) }

doubleQuotedString = _{ "\"" ~ doubleInner ~ "\"" }
doubleInner = @{ doubleChar* }
doubleChar = {
    !("\"" | "\\" | '\u{00}'..'\u{1F}') ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ upperHexDigit{4})
}
upperHexDigit = _{ ASCII_DIGIT | "A" | "B" | "C" | "D" | "E" | "F" }

singleQuotedString = _{ "'" ~ singleInner ~ "'" }
singleInner = @{ singleChar* }
singleChar = {
    !("'" | "\\" | '\u{00}'..'\u{1F}') ~ ANY
    | "\\" ~ ("'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ upperHexDigit{4})
}

WHITESPACE = _{ " " }